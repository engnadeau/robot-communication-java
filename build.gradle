plugins {
    // https://github.com/google/osdetector-gradle-plugin
    id "com.google.osdetector" version "1.4.0"
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group 'me.nicholasnadeau'

repositories {
    mavenCentral()

    maven { url 'https://jitpack.io' }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    if (osdetector.classifier != 'windows-x86_32') {
        // http://mvnrepository.com/artifact/io.netty/netty-tcnative
        compile group: 'io.netty', name: 'netty-tcnative', version: '1.1.33.Fork15', classifier: osdetector.classifier
    }

    // server-client
    // https://mvnrepository.com/artifact/io.netty/netty-all
    compile group: 'io.netty', name: 'netty-all', version: '4.1.9.Final'

    // https://jitpack.io/#nnadeau/robot-communication-packet-proto
    compile 'com.github.nnadeau:robot-communication-packet-proto:0.3.0'
}

if (project.hasProperty('tag')) {
    version = project.tag
} else {
    version = '0'
}

shadowJar.archiveName = "$project.name-$project.version-all.jar"

// https://docs.gradle.org/current/userguide/java_plugin.html#sec:java_cross_compilation
println System.properties.'java.home'
sourceCompatibility = 1.6
if (System.properties.'java.home'.contains('1.6') || System.properties.'java.home'.contains('java-6')) {
    def javaExecutablesPath = new File(System.properties.'java.home', 'bin')
    def javaExecutables = [:].withDefault { execName ->
        def executable = new File(javaExecutablesPath, execName)
        assert executable.exists(): "There is no ${execName} executable in ${javaExecutablesPath}"
        executable
    }
    tasks.withType(AbstractCompile) {
        options.with {
            fork = true
            forkOptions.executable = javaExecutables.javac
        }
    }
    tasks.withType(Javadoc) {
        executable = javaExecutables.javadoc
    }
    tasks.withType(Test) {
        executable = javaExecutables.java
    }
    tasks.withType(JavaExec) {
        executable = javaExecutables.java
    }
}
